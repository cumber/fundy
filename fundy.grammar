
# ignore whitespace or continuation
IGNORE:     "[ \t]+|\\\n";

NUMBER:     "[0-9]+";
IDENT:      "[a-zA-Z_0-9+\-*/=!@$%\^&\|]+";
STRING:     "\"([ a-zA-Z0-9!@\#$%\^&*\(\)\-=_+\|\[\]\{\}'\;:,.<>/\?`~]|\\\"|\\n|\\t|\\b|\\v|\\0|\\\\)*\"";
CHAR:       "'([ a-zA-Z0-9!@\#$%\^&*\(\)\-=_+\|\[\]\{\}\"\;:,.<>/\?`~]|\\'|\\n|\\t|\\b|\\v|\\0|\\\\)'";
LINEBREAK:  "\n[ \t\n]*";
MISSING:    "\?[a-zA-Z_0-9+\-*/=!@$%\&\|\?]+";


program:    [LINEBREAK]? (statement [LINEBREAK])* statement? [EOF] ;

statement:  <io_statement>
    |       <bind_statement>
    ;

# statements that do IO, and therefore cannot be allowed inside a function
io_statement:   <print_statement>
    |           <show_statement>
    ;

print_statement:    ["print"]? expr ( [","] expr )* [LINEBREAK] ;

show_statement:     ["show"] ( expr ( [","] expr )* )? [LINEBREAK] ;

# statements that bind names, which can be allowed inside a function
bind_statement:     <def_statement>
    |               <assign_statement>
    ;

assign_statement:     IDENT ["="] expr [LINEBREAK] ;

# the def statement defines a function and binds it to a name
def_statement:      ["def"] IDENT param* type_decl? [LINEBREAK]
                    block [LINEBREAK] ;

block:      bind_statement* ["return"] expr ;

param:  IDENT type_decl? ;

type_decl:          ["::"] "type" ;



expr:   >chain< ;

term:   <IDENT>
    |   <MISSING>
    |   <literal>
    |   <paren>
    ;

paren:  "(" <expr> ")" ;

chain:  term+;

literal:    <NUMBER>
    |       <list>
    |       <tuple>
    |       <STRING>
    |       <CHAR>
    ;

list:   [ "[" ] ( expr ( [","] expr )* tail? )? [ "]" ] ;

tail:   [ ":" ] expr ;

tuple:  ["{"] ( expr ( [","] expr )+ )? ["}"]
    |   ["{"] expr [","] ["}"]
    ;
